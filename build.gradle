buildscript {
    def esVersion = System.getProperty('esVersion', '5.5.2')

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.elasticsearch.gradle:build-tools:${esVersion}"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.1.3-2'
    id 'com.google.protobuf' version '0.8.10'
}

apply plugin: 'idea'
apply plugin: 'elasticsearch.esplugin'

esplugin {
    name = 'mapper-external-file'
    description 'External file field mapper for ElasticSearch'
    classname = 'company.evo.elasticsearch.plugin.mapper.ExternalFileMapperPlugin'
}

def version = project.file('project.version')
        .newDataInputStream()
        .newReader()
        .readLine()
        .toUpperCase()
if (hasProperty('release') && version.endsWith('-SNAPSHOT')) {
    version = version.substring(0, version.length() - 9)
}
project.version = "${version}-es${versions.elasticsearch}"

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib"
    compile "org.apache.httpcomponents:httpcore:4.4.13"
    compile "org.apache.httpcomponents:httpclient:4.5.12"
    compile "commons-logging:commons-logging:${versions.commonslogging}"
    compile "com.google.protobuf:protobuf-java:3.12.4"
    compile "net.sf.trove4j:trove4j:3.0.3"
    compile project(':htable-java')
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.12.4'
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

task testFunc(type: Test) {
    include '**/*TestCase.class'
}

testFunc.outputs.upToDateWhen { false }
