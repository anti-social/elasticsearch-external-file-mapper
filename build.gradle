buildscript {
    def esVersion = System.getProperty('esVersion', '5.5.2')

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.elasticsearch.gradle:build-tools:${esVersion}"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.2.0'
}

allprojects {
    apply plugin: 'kotlin'
}
apply plugin: 'idea'
apply plugin: 'elasticsearch.esplugin'

esplugin {
    name = 'mapper-external-file'
    description 'External file field mapper for ElasticSearch'
    classname = 'company.evo.elasticsearch.plugin.mapper.ExternalFileMapperPlugin'
}

def version = project.file('project.version')
        .newDataInputStream()
        .newReader()
        .readLine()
        .toUpperCase()
if (hasProperty('release') && version.endsWith('-SNAPSHOT')) {
    version = version.substring(0, version.length() - 9)
}
project.version = "${version}-es${versions.elasticsearch}"

repositories {
    mavenCentral()
}

subprojects {
    repositories {
        mavenCentral()
    }
    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib"
        compile "org.apache.httpcomponents:httpcore:${versions.httpcore}"
        compile "org.apache.httpcomponents:httpclient:${versions.httpclient}"
//        compile "commons-logging:commons-logging:${versions.commonslogging}"
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = '1.8'
        }
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib"
    compile "commons-logging:commons-logging:${versions.commonslogging}"
}

tasks.integTestRunner.doFirst {
    System.setProperty('user.dir', './temp')
    println "user.dir: ${System.getProperty('user.dir')}"
}
//integTest {
////    doFirst {
////    }
//}
